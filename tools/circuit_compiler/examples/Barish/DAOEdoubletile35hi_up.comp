declare component DAOEdoubletile35hi_up: sLL + sLM -> sLR + sUR + sinert
# bottom left tile is 3up, upper right tile is 5up
# bottom tile's upper-left sticky end is a hairpin; top tile's upper-left sticky end is inert
# just like Barish et al's Z78 for Copy (but not Variable-Width)

## Sequences
# sticky ends
sequence sLL = "5N" : 5
sequence sLM = "5N" : 5
sequence sLR = "5N" : 5
sequence sUR = "5N" : 5
sequence sinert = "5N" : 5

# core 
sequence sbULhairpin_arm = "7N" : 7
sequence sbULarm = "S 7N" : 8
sequence sbLLarm = "S 7N" : 8 
sequence sbLRarm = "S 7N" : 8
sequence sbUcore = "S 14N S" : 16
sequence sbLcore = "S 14N S" : 16
sequence sMarm   = "21N" : 21 
sequence stULarm = "S 7N" : 8
sequence stLRarm = "S 7N" : 8 
sequence stURarm = "S 7N" : 8
sequence stUcore = "S 14N S" : 16
sequence stLcore = "S 14N S" : 16

## Strands
strand shortLeft    = sLL sbLLarm sbULarm* sbULhairpin_arm "TTTT" sbULhairpin_arm* : 39
strand longLeft     = sbULarm sbUcore sbLcore* sbLLarm* : 48
strand longMidLeft  = sinert stULarm sMarm sbUcore* sbLcore sbLRarm* : 74
strand longMidRight = sLM sbLRarm sMarm* stLcore stUcore* stULarm* : 74
strand longRight    = stLRarm stLcore* stUcore stURarm* : 48
strand shortRight   = sUR stURarm stLRarm* sLR : 26

## Structures
structure [no-opt] tile = shortLeft + longLeft + longMidLeft + longMidRight + longRight + shortRight : 39. + 48. + 74. + 74. + 48. + 26.  
# This is a dummy structure that tells the designer that these strands are in a 
#   complex, but provides no further base-pairing information because we don't 
#   "yet" handle psuedoknots
