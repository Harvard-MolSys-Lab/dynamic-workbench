declare component DAOEdoubletile35hi_up: sLL + sLM -> sLR + sUR + sinert
# bottom left tile is 3up, upper right tile is 5up
# bottom tile's upper-left sticky end is a hairpin; top tile's upper-left sticky end is inert
# just like Barish et al's Z78 for Copy (but not Variable-Width)

## Sequences
# sticky ends
sequence sLL = "5N" : 5
sequence sLM = "5N" : 5
sequence sLR = "5N" : 5
sequence sUR = "5N" : 5
sequence sinert = "5N" : 5

# core 
sequence sbULhairpin_arm = "7N" : 7
sequence sbULarm = "N 7N" : 8
sequence sbLLarm = "N 7N" : 8 
sequence sbLRarm = "N 7N" : 8
sequence sbUcore = "N 14N N" : 16
sequence sbLcore = "N 14N N" : 16
sequence sMarm   = "21N" : 21 
sequence stULarm = "N 7N" : 8
sequence stLRarm = "N 7N" : 8 
sequence stURarm = "N 7N" : 8
sequence stUcore = "N 14N N" : 16
sequence stLcore = "N 14N N" : 16

## Strands
strand shortLeft    = sLL sbLLarm sbULarm* sbULhairpin_arm "TTTT" sbULhairpin_arm* : 39
strand longLeft     = sbULarm sbUcore sbLcore* sbLLarm* : 48
strand longMidLeft  = sinert stULarm sMarm sbUcore* sbLcore sbLRarm* : 74
strand longMidRight = sLM sbLRarm sMarm* stLcore stUcore* stULarm* sinert* : 79
strand longRight    = stLRarm stLcore* stUcore stURarm* : 48
strand shortRight   = sUR stURarm stLRarm* sLR : 26

## Structures
structure [no-opt] tile = shortLeft + longLeft + longMidLeft + longMidRight + longRight + shortRight : 39. + 48. + 74. + 79. + 48. + 26.  
# This is a dummy structure that tells the designer that these strands are in a 
#   complex, but provides no further base-pairing information because we don't 
#   "yet" handle psuedoknots
