declare component DAOEdoubletile35ii_up: sLL + sLM -> sLR + sUR + sinert1 + sinert2
# bottom left tile is 3up, upper right tile is 5up
# bottom tile's upper-left sticky end is inert; top tile's upper-left sticky end is also inert
# just like Barish et al's Z78 for Variable-Width (but not Copy)

## Sequences
# sticky ends
sequence sLL = "5N" : 5
sequence sLM = "5N" : 5
sequence sLR = "5N" : 5
sequence sUR = "5N" : 5
sequence sinert1 = "5N" : 5
sequence sinert2 = "5N" : 5

# core 
sequence sbULarm = "S 7N" : 8
sequence sbLLarm = "S 7N" : 8 
sequence sbLRarm = "S 7N" : 8
sequence sbUcore = "S 14N S" : 16
sequence sbLcore = "S 14N S" : 16
sequence sMarm   = "21N" : 21 
sequence stULarm = "S 7N" : 8
sequence stLRarm = "S 7N" : 8 
sequence stURarm = "S 7N" : 8
sequence stUcore = "S 14N S" : 16
sequence stLcore = "S 14N S" : 16

## Strands
strand shortLeft    = sLL sbLLarm sbULarm* sinert1 : 26
strand longLeft     = sbULarm sbUcore sbLcore* sbLLarm* : 48
strand longMidLeft  = sinert2 stULarm sMarm sbUcore* sbLcore sbLRarm* : 74
strand longMidRight = sLM sbLRarm sMarm* stLcore stUcore* stULarm* : 74
strand longRight    = stLRarm stLcore* stUcore stURarm* : 48
strand shortRight   = sUR stURarm stLRarm* sLR : 26

## Structures
structure [no-opt] tile = shortLeft + longLeft + longMidLeft + longMidRight + longRight + shortRight : 26. + 48. + 74. + 74. + 48. + 26.  
# This is a dummy structure that tells the designer that these strands are in a 
#   complex, but provides no further base-pairing information because we don't 
#   "yet" handle psuedoknots
