declare component UPG2Bit: input -> output


################
# SEQUENCES
################
sequence toehold 	= "?N" : 5

sequence input 		= "?N" : 15
sequence gatea		= "?N" : 15
sequence gateb		= "?N" : 15

sequence sigi0		= "?N" : 15
sequence sigi1		= "?N" : 15
sequence switchi0	= "?N" : 15
sequence switchi1	= "?N" : 15

sequence output		= "?N" : 15



################
# STRANDS
################

# Input signals
strand st_in_signal		= input toehold gatea toehold : 40
strand st_i0_signal		= toehold sigi0 : 20
strand st_i1_signal		= toehold sigi1 : 20

# Gates A1/A2
strand st_gatea1_output	= gatea toehold switchi0 toehold : 40
strand st_gatea1_base	= toehold* gatea* toehold* : 25

strand st_gatea2_output	= gatea toehold switchi1 toehold : 40
strand st_gatea2_base	= toehold* gatea* toehold* : 25

# Gates B1/B2
strand st_gateb1_output	= gateb toehold output toehold : 40
strand st_gateb1_base	= toehold* gateb* toehold* : 25

strand st_gateb2_output	= gateb toehold : 20
strand st_gateb2_base	= toehold* gateb* toehold* : 25

# Gate I0/I1
strand st_gatei0_switch = sigi0 toehold : 20
strand st_gatei0_output = switchi0 toehold gateb toehold : 40
strand st_gatei0_base	= toehold* switchi0* toehold* sigi0* toehold* : 45

strand st_gatei1_switch = sigi1 toehold : 20
strand st_gatei1_output = switchi1 toehold output toehold : 40
strand st_gatei1_base	= toehold* switchi1* toehold* sigi1* toehold* : 45

# Ouput Reporter
strand st_output		= output toehold : 20
strand st_output_base	= toehold* output* toehold* : 25



################
# STRUCTURES
################

# Signals
structure In_Signal			= st_in_signal : domain ....
structure I0_Signal			= st_i0_signal : domain ..
structure I1_Signal			= st_i1_signal : domain ..

# Gates
structure Gatea1			= st_gatea1_output + st_gatea1_base : domain ((..+)).
structure Gatea2			= st_gatea2_output + st_gatea2_base : domain ((..+)).
structure Gateb1			= st_gateb1_output + st_gateb1_base : domain ((..+)).
structure Gateb2			= st_gateb2_output + st_gateb2_base : domain ((+)).
structure Gatei0			= st_gatei0_switch + st_gatei0_output + st_gatei0_base : domain ((+((..+)))).
structure Gatei1			= st_gatei1_switch + st_gatei1_output + st_gatei1_base : domain ((+((..+)))).

# Waste
structure Gatea1_Waste		= st_in_signal + st_gatea1_base : domain .(((+)))
structure Gatea2_Waste		= st_in_signal + st_gatea2_base : domain .(((+)))
structure Gateb1_Waste		= st_gatei0_output + st_gateb1_base : domain .(((+)))
structure Gateb2_Waste		= st_gatei0_output + st_gateb2_base : domain .(((+)))

structure Gatei0_Interm		= st_i0_signal + st_gatei0_output + st_gatei0_base : domain ((+((..+)).))
structure Gatei1_Interm		= st_i1_signal + st_gatei1_output + st_gatei1_base : domain ((+((..+)).))
structure Gatei0_Waste1		= st_gatei0_switch : domain ..
structure Gatei1_Waste1		= st_gatei1_switch : domain ..
structure Gatei0_Waste2		= st_i0_signal + st_gatei0_output + st_gatei0_base : domain ((+.(((+)))))
structure Gatei1_Waste2		= st_i1_signal + st_gatei1_output + st_gatei1_base : domain ((+.(((+)))))

structure Outputb1_Waste	= st_gateb1_output + st_output_base : domain .(((+)))
structure Outputi1_Waste	= st_gatei1_output + st_output_base : domain .(((+)))

# Outputs
structure Gatea1_Output		= st_gatea1_output : domain ....
structure Gatea2_Output		= st_gatea2_output : domain ....
structure Gateb1_Output		= st_gateb1_output : domain ....
structure Gateb2_Output		= st_gateb2_output : domain ..
structure Gatei0_Output		= st_gatei0_output : domain ....
structure Gatei1_Output		= st_gatei1_output : domain ....


# Final Outputs
structure Output_Reporter	= st_output + st_output_base : domain ((+)).
structure Output_Final		= st_output : domain ..


################
# KINETICS
################
# SplitPSwitch A
kinetic In_Signal + Gatea1 -> Gatea1_Waste + Gatea1_Output
kinetic In_Signal + Gatea2 -> Gatea2_Waste + Gatea2_Output

# DSwitch I0
kinetic Gatea1_Output + Gatei0 -> Gatei0_Waste1 + Gatei0_Interm
kinetic I1_Signal + Gatei1_Interm -> Gatei1_Waste2 + Gatei1_Output

# DSwitch I1
kinetic Gatea2_Output + Gatei1 -> Gatei1_Waste1 + Gatei1_Interm
kinetic I0_Signal + Gatei0_Interm -> Gatei0_Waste2 + Gatei0_Output

# PSwitch B
kinetic Gatei0_Output + Gateb1 -> Gateb1_Waste + Gateb1_Output
kinetic Gatei0_Output + Gateb2 -> Gateb2_Waste + Gateb2_Output

# Output Reporter
kinetic Gateb1_Output + Output_Reporter -> Outputb1_Waste + Output_Final
kinetic Gatei1_Output + Output_Reporter -> Outputi1_Waste + Output_Final
